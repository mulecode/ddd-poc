plugins {
    id 'idea'
    id 'java'
    id 'groovy'
    id 'org.springframework.boot' version '3.4.0'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'org.jetbrains.kotlin.jvm' version '2.0.21'
    id 'org.jetbrains.kotlin.plugin.spring' version '2.0.21'
    id 'org.jetbrains.kotlin.plugin.jpa' version '2.0.21'
}

group = 'uk.co.mulecode.ddd'
version = '1.0.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

kotlin {
    jvmToolchain(21)
    compilerOptions {
        freeCompilerArgs.addAll '-Xjsr305=strict'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin'
    implementation 'org.jetbrains.kotlin:kotlin-reflect'
    implementation 'io.github.oshai:kotlin-logging-jvm:6.0.9'
    implementation 'com.h2database:h2'
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.flywaydb:flyway-mysql'
    implementation 'org.codehaus.groovy:groovy:3.0.19'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.8.1"

    runtimeOnly 'com.mysql:mysql-connector-j:8.4.0'

    testImplementation "org.testcontainers:spock:1.20.4"
    testImplementation "org.testcontainers:mysql:1.20.4"
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.spockframework:spock-core:2.4-M4-groovy-3.0'
    testImplementation 'org.spockframework:spock-spring:2.4-M4-groovy-3.0'

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

allOpen {
    annotation 'jakarta.persistence.Entity'
    annotation 'jakarta.persistence.MappedSuperclass'
    annotation 'jakarta.persistence.Embeddable'
}

tasks.withType(Test).configureEach {
    maxParallelForks = Runtime.runtime.availableProcessors()
    useJUnitPlatform()
}

tasks.named('jar') {
    enabled = false
}

tasks.named('bootJar') {
    enabled = true
}

tasks.register('unitTest', Test) {
    description = "Runs unit tests"
    group = "verification"
    useJUnitPlatform {
        includeTags 'unit'
    }
    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath
}

tasks.register('integrationMinTest', Test) {
    description = "Runs minimal integration tests"
    group = "verification"
    useJUnitPlatform {
        includeTags 'integration-min'
    }
    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath
}

tasks.register('integrationTest', Test) {
    description = "Runs full integration tests"
    group = "verification"
    useJUnitPlatform {
        includeTags 'integration'
    }
    testClassesDirs = sourceSets.test.output.classesDirs
    classpath = sourceSets.test.runtimeClasspath
}
